#!/usr/bin/env zsh

curdir=$PWD
if which mighty >/dev/null; then
    cd _site && \
    mighty +RTS -N >/dev/null 2>&1 &
elif which http-server; then
    cd _site && \
    http-server >/dev/null 2>&1 &
else
    print -P -- "%BWARNING%b: Please serve _site/ directory with an HTTP Server"
    print -P -- "%BWARNING%b: You might install http-server or mighty for example"
    print
fi
cd $curdir

# sass
sass --watch Scratch/css:_site/Scratch/css &

checkfile=".last_multi_watched"

ONCE=0
[[ $1 = "once" ]] && { ONCE=1 }

error(){print -- $* >&2; exit 1}

((ONCE == 0)) && \
[[ -e $checkfile ]] && \
{
currenttime=$(date +"%s")
case $(uname) in
    Darwin) checkfilemodifiedtime=$(stat -f %m $checkfile);;
    Linux) checkfilemodifiedtime=$(stat --printf %Y $checkfile);;
    *) checkfilemodifiedtime=$(stat --printf %Y $checkfile);;
esac
(( $currenttime - $checkfilemodifiedtime  < 5)) && error "watching in progress (wait at least 5 seconds if you killed it)"
}

packageconf=( $(print -- .cabal-sandbox/*.conf.d(N)) )
((${#packageconf} == 0 )) && error "Please, wait the end of compilation and relaunch preview again"
((${#packageconf} > 1 )) && {
    print -- "I need to use only one directory"
    print -l -- $packageconf
    exit 1
}>&2

languages=( $(runghc -package-db=$packageconf showlangs.hs) )
((${#languages} == 0)) && error "Couldn't retrieve languages [runghc -package-db=$packageconf showlang.hs]"
print "languages to watch (in Config.hs) are: $languages"
typeset -a exclu

while true; do
  for language in $languages; do
    exclu=()
    for l in $languages; do
      case $l in
        $language) continue ;;
      esac
      exclu=( $exclu $l )
    done
    for fic in multi/**/*(.); do
      dest=Scratch/$language/${fic#multi/}
      [[ ! -e $dest || $fic -nt $dest ]] || continue
      print $dest
      [[ ! -d ${dest:h} ]] && mkdir ${dest:h}
      awkprg='! /^('$exclu'): / { sub(/^'$language': /,""); print $0 }'
      awk "$awkprg" < $fic > $dest
      ./.cabal-sandbox/bin/yblog build
    done
  done
  ((ONCE == 1)) && break
  touch $checkfile
  sleep 2
done
